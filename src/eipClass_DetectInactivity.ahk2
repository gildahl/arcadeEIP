; arcadeEIP (eip.exe) - Arcade Emulator Integration Player
; Â© 2023 David G. Dahlstrom
; File: eipClass_DetectInactivity.ahk2

;This class monitors the system for inactivity (user NOT pressing buttons
;moving mouse, moving joysticks, etc), for the purpose of determining when
;auto-switching is allowed to happen.
class DetectInactivity_Class
{
	__New(callback, delay, unmute)
	{
		this._unmute := unmute ;unmute on activity setting
		this._count := 0 ;counts calls to support calls from independent threads
		this._delay := delay
		this._callback := callback ;normally, the AttractSwitch() function
		this._fInactivityTest := ObjBindMethod(this, "_inactivity_test")
		this._action_count := 0 ;used to count activity actions
		this._force_action := false
	}

	ActionCount
	{
		get => this._action_count
		set => this._action_count := value
	}

    delay
	{
		get => this._delay
		set => this._delay := value
	}
	
	Activate()
	{
		global
		local id, mJoylist, mState, fInactivityTest, numButtons

		mJoylist := Map()

		;delay must be >= 20 seconds, otherwise delay
		;is considered set to infinity
		if (this._delay < 20)
			this._delay := 3000000000 ;about 100 years should be a good approximation for infinity

		Loop 16
		{
			if (GetKeyState(A_Index "JoyName"))
			{	
				id := A_Index
				
				mState := Map()
				mState["id"] := id
				mState["X"] := GetKeyState(id "JoyX")
				mState["Y"] := GetKeyState(id "JoyY")
				
				mJoylist[id] := mState	

				numButtons := GetKeyState(id "JoyButtons")
				if (numButtons > 16) ;ahk can't seem to handle more than 16 buttons
					numButtons := 16

				Loop numButtons
				{
					HotKey "*~" . A_Index . "Joy" . id, ObjBindMethod(this, "_joy_button_action")
				}
			}
		}
		
		this._joylist := mJoylist
		
		this._last_idle_time := A_TimeIdlePhysical 
		fInactivityTest := this._fInactivityTest
		SetTimer fInactivityTest, cInactivityTest_Period ;1000 ms	
		
		return
	}

	Resume()
	{
		;only resume if all other suspensions have been resumed
		this._count--
		if (this._count > 0)
			return
		
		this._last_idle_time := A_TimeIdlePhysical 
		fInactivityTest := this._fInactivityTest
		SetTimer fInactivityTest, cInactivityTest_Period

		return
	}

	;A method to "poke" the object to simulate an activty
	;Used by the Schedule class
	Poke()
	{
		this._last_idle_time := A_TimeIdlePhysical
	}

	Suspend()
	{
		;keep track nested calls
		this._count++
		if (this._count > 1)
			return

		fInactivityTest := this._fInactivityTest
		SetTimer fInactivityTest, 0

		return
	}
	
	Inactivate()
	{
		fInactivityTest := this._fInactivityTest
		SetTimer fInactivityTest, 0
		return
	}

	ForceAction()
	{
		this._force_action := true
	}

	;Obtain joystick state
	GetJoyState()
	{
		mJoyX := Map()
		mJoyY := Map()

		horiz := gCfg.joy_horiz == "all" ? 1 : 0
    	vert := gCfg.joy_vert == "all" ? 1 : 0

		mJoylist := this._joylist

		for n, mState in mJoylist
		{
			id := mState["id"]

			mJoyX[id] := GetKeyState(id "JoyX")
			mJoyY[id] := GetKeyState(id "JoyY")

			if (mJoyX[id] - mState["X"] > 20 && horiz)
				return "r"
			if (mJoyX[id] - mState["X"] < -20 && horiz)
				return "l"
			if (mJoyY[id] - mState["Y"] > 20 && vert)
				return "d"
			if (mJoyY[id] - mState["Y"] < -20 && vert)
				return "u"
		}

		return
	}
	
	_inactivity_test(theHotkey:="")
	{
		mJoylist := this._joylist
		sound_set := false
		joy_test := "None detected"
		logEntry := ""
		mJoyX := Map()
		mJoyY := Map()

		;Whenever idle time drops below 1000, it must have been reset
		;by the press of a keyboard or mouse button.
		if (A_TimeIdlePhysical < 1000)
		{
			this._last_idle_time := A_TimeIdlePhysical

			;used to monitor activity by external frequency monitor
			this._action_count++ 

			if (this._unmute)
				MasterSound.On() ;force sound unmute

			if (gCfg.idle_log)
			{
				t := FormatTime(A_Now, "hh:mm:ss")
				logEntry := "IdleInterrupted (key/mouse): [" . t . "]"			
			}
		}
		
		for n, mState in mJoylist
		{
			id := mState["id"]

			x := mJoyX[id] := GetKeyState(id "JoyX")
			y := mJoyY[id] := GetKeyState(id "JoyY")
		
			if ((Abs(mJoyX[id] - mState["X"]) > 20) || Abs(mJoyY[id] - mState["Y"]) > 20)
			{
				this._action_count++
				this._last_idle_time := A_TimeIdlePhysical

				if (!sound_set && this._unmute) ;only do once per test
				{
					sound_set := true
					MasterSound.On() ;force sound unmute
				}
				
				if (gCfg.idle_log)
				{
					joy_test := "X:" . x .  "  Y:" . y 
					
					t := FormatTime(A_Now, "hh:mm:ss")
					logEntry := "IdleInterrupted (joy): [" t "] JoyTest: " joy_test
				}
				break
			}
		}

		if (gCfg.idle_log && logEntry)
		{
			IdleLogEntry(logEntry)
			logEntry := ""
		}

		if (gCfg.idle_log && logEntry)
			IdleLogEntry(logEntry)

		;The delay time has been reached, so perform the action to be done
		timeIdlePhysical := A_TimeIdlePhysical
		if (timeIdlePhysical - this._last_idle_time > this._delay * 1000 || this._force_action) 
		{
			this._force_action := false
			this._inactivity_action()
		}

		;Useful timer debug code
		;lit := this._last_idle_time
		;diff := timeIdlePhysical - lit
		;OutputDebug LastIdleTime: %lit%  TimeIdlePhysical: %timeIdlePhysical%  Diff: %diff% `n

		return
	}
	
	_inactivity_action()
	{
		this._last_idle_time := A_TimeIdlePhysical 

		Critical "On"
		this._callback.Call()	;normally, the AttractSwitch() function
		Critical "Off"

		return
	}
	
	_joy_button_action(caller:="")
	{
		;a joystick button was pressed, so reset the last idle time
		this._last_idle_time := A_TimeIdlePhysical
		return
	}
}